
Процедура РазобратьПараметрЗапуска(ПараметрЗапускаСистемы) Экспорт
	
	Если ПустаяСтрока(ПараметрЗапускаСистемы) Тогда
		Возврат;
	Иначе
		ПарамЗап = ВРег(СокрЛП(ПараметрЗапускаСистемы));
	КонецЕсли;
	
	НужноЗавершитьРаботу = Поз(ПарамЗап, ВРег("ЗавершитьРаботуСистемы"))<>0;
	НужноПроверитьМодифицированностьКонфигурации = Поз(ПарамЗап, ВРег("ПроверитьМодифицированностьКонфигурации"))<>0;
	НужноОбновитьМетаданные = Поз(ПарамЗап, ВРег("ЗапуститьОбновлениеИнформационнойБазы"))<>0;
	
	СигналЛога = ВРег("РасширенныйЛогФайл=");
	ПозицияСигнала = Поз(ПарамЗап, СигналЛога);
	Если ПозицияСигнала<>0 Тогда
		
		РасширенныйЛогФайл = Сред(ПарамЗап, ПозицияСигнала + СтрДлина(СигналЛога));
		
		ПозНачалаИмениФайла = Поз(РасширенныйЛогФайл, "[");
		Если ПозНачалаИмениФайла<>0 Тогда
			РасширенныйЛогФайл = Сред(РасширенныйЛогФайл, ПозНачалаИмениФайла + 1);
			ПозКонцаИмениФайла = Поз(РасширенныйЛогФайл, "]");
			Если ПозКонцаИмениФайла<>0 Тогда
				РасширенныйЛогФайл = Лев(РасширенныйЛогФайл, ПозКонцаИмениФайла - 1)
			Иначе
				РасширенныйЛогФайл = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации("Jenkins.Параметры запуска внешней обработки", УровеньЖурналаРегистрации.Информация, , , "НужноЗавершитьРаботу: " + НужноЗавершитьРаботу);
	ЗаписьЖурналаРегистрации("Jenkins.Параметры запуска внешней обработки", УровеньЖурналаРегистрации.Информация, , , "НужноПроверитьМодифицированностьКонфигурации: " + НужноПроверитьМодифицированностьКонфигурации);
	ЗаписьЖурналаРегистрации("Jenkins.Параметры запуска внешней обработки", УровеньЖурналаРегистрации.Информация, , , "НужноОбновитьМетаданные: " + НужноОбновитьМетаданные);
	ЗаписьЖурналаРегистрации("Jenkins.Параметры запуска внешней обработки", УровеньЖурналаРегистрации.Информация, , , "РасширенныйЛогФайл: " + РасширенныйЛогФайл);
		
КонецПроцедуры

Функция Поз(ГдеИщем, ЧтоИщем)
	Попытка
		Возврат Вычислить("СтрНайти(ГдеИщем, ЧтоИщем)")
	Исключение
		Возврат Найти(ГдеИщем, ЧтоИщем)
	КонецПопытки;
КонецФункции

//////////////////
// инит

ЗаписьЖурналаРегистрации("Jenkins.Инициализация внешней обработки", УровеньЖурналаРегистрации.Информация, , , "Начало");
КонфигурацияМодифицирована = КонфигурацияИзменена();
Попытка
	Модуль = ОбщегоНазначения.ОбщийМодуль("ОбновлениеКонфигурацииПереопределяемый");
	Модуль.ПриОпределенииКраткогоИмениКонфигурации(КраткоеИмяКонфигурации);
Исключение
	Попытка
		Модуль = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователейПереопределяемый");
		Модуль.ПриОпределенииИмениПрограммы(КраткоеИмяКонфигурации);
	Исключение
		КраткоеИмяКонфигурации = "UNKNOWN_CONFIG_NAME";
	КонецПопытки;
КонецПопытки;

ВерсияКонфигурации = Метаданные.Версия;

ЗаписьЖурналаРегистрации("Jenkins.Инициализация внешней обработки", УровеньЖурналаРегистрации.Информация, , , "Завершение");
СистемнаяИнформация = Новый СистемнаяИнформация();
Платформа = СистемнаяИнформация.ВерсияПриложения;
